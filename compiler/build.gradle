plugins {
    id('java')
    id('com.github.johnrengelman.shadow') version('6.1.0')
    id('maven-publish')
    id('signing')
}

group = 'io.github.jbock-java'

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(15))
    }
}

compileJava {
    options.encoding = 'UTF-8'
    sourceCompatibility = '11'
    targetCompatibility = '11'
    options.compilerArgs << '--release'
    options.compilerArgs << '11'
}

repositories {
    mavenCentral()
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.withType(GenerateModuleMetadata) {
    enabled = true
}

shadowJar {
    minimize()
    dependencies {
        exclude('javax.annotation:jsr250-api:.*')
    }
    archiveClassifier.set('')
    relocate('com.squareup.javapoet', 'dapper.javapoet')
    relocate('com.google', 'dapper.google')
    relocate('org.checkerframework', 'dapper.org.checkerframework')
    relocate('javax.inject', 'dapper.javax.inject')
}

dependencies {
    def core = project(':core')
    implementation('com.squareup:javapoet:1.13.0')
    implementation('com.google.auto:auto-common:1.0')
    implementation('com.google.guava:guava:30.1.1-jre')
    implementation('com.google.googlejavaformat:google-java-format:1.11.0')
    shadow(core)
    implementation('com.google.auto.value:auto-value-annotations:1.8.2')
    annotationProcessor('com.google.auto.value:auto-value:1.8.2')
    implementation('org.checkerframework:checker-qual:3.18.1')
    implementation('org.checkerframework:checker-compat-qual:2.5.5')
    annotationProcessor('org.checkerframework:checker:3.18.1')
    implementation('com.google.dagger:dagger:2.36')
    annotationProcessor('com.google.dagger:dagger-compiler:2.36')
    implementation('javax.annotation:jsr250-api:1.0')
    testImplementation(core)
    testImplementation('com.google.testing.compile:compile-testing:0.19')
    testImplementation('org.junit.jupiter:junit-jupiter:5.8.1')
    testImplementation('org.junit.vintage:junit-vintage-engine:5.8.1')
    testImplementation('org.mockito:mockito-core:3.12.4')
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': 'dapper.compiler',
                'Implementation-Version': project.properties['version']
        )
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events('failed')
    }
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

artifacts {
    archives(javadocJar, sourcesJar)
}

// https://central.sonatype.org/pages/gradle.html
publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'dapper-compiler'

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'dapper-compiler'
                packaging = 'jar'
                description = 'dapper annotation processor'
                url = 'https://github.com/jbock-java/dapper'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'Various'
                        name = 'Various'
                        email = 'jbock-java@gmx.de'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jbock-java/dapper.git'
                    developerConnection = 'scm:git:https://github.com/jbock-java/dapper.git'
                    url = 'https://github.com/jbock-java/dapper'
                }
            }
        }
    }
    repositories {
        maven {
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = System.getenv('OSS_USER')
                password = System.getenv('OSS_PASS')
            }
        }
    }
}

// https://docs.gradle.org/current/userguide/signing_plugin.html
signing {
    def signingKey = findProperty('signingKey')
    def signingPassword = findProperty('signingPassword')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
